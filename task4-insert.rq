PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?category (COUNT(distinct ?recipe) as ?n) (AVG(?cal) as ?energy) (AVG(?fatNum) as ?fat) (AVG(?sat) as ?satfat) (AVG(?sugars) as ?sugar) (AVG(?salt) as ?sodium) (AVG(?ratingValue) as ?rating) (SUM(?ratingCount) as ?numOfRatings) (AVG(?HealthScore) as ?Score) WHERE {
    ?node schema:name ?recipe .
    ?node schema:recipeCategory ?category.
    FILTER (?category IN ("Ingredients", "Lunch Recipes", "Main Dishes", "Meat and Poultry"))
    ?node schema:nutrition ?nutritionNode .
  	?node schema:HealthScore ?HealthScore .
  
  	?nutritionNode schema:calories ?calories .
    BIND(strbefore(?calories, " ") as ?calStr)
    BIND(xsd:double(?calStr) as ?cal)
  
  	?nutritionNode schema:sugarContent ?sugarContent .
    BIND(strbefore(?sugarContent, " ") as ?sugarStr)
    BIND(xsd:double(?sugarStr) as ?sugars)

    ?nutritionNode schema:sodiumContent ?saltContent .
    BIND(strbefore(?saltContent, " ") as ?saltStr)
    BIND(xsd:double(?saltStr)/1000 as ?salt)

    ?nutritionNode schema:fatContent ?fatContent .
    BIND(strbefore(?fatContent, " ") as ?fatStr)
    BIND(xsd:double(?fatStr) as ?fatNum)

    ?nutritionNode schema:saturatedFatContent ?satContent .
    BIND(strbefore(?satContent, " ") as ?satStr)
    BIND(xsd:double(?satStr) as ?sat)
  
  	?node schema:aggregateRating ?ratingNode .
  	?ratingNode schema:ratingValue ?ratingValue .
  	?ratingNode schema:ratingCount ?ratingCount .
  
} group by ?category