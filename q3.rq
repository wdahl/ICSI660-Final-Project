PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT distinct ?recipe (?score7 as ?HealthSocre) WHERE {
    ?node schema:name ?recipe .
    filter (?recipe in ("Mayo Chicken"))
    ?node schema:nutrition ?nutritionNode .

    ?nutritionNode schema:proteinContent ?proteinContent .
    BIND(strbefore(?proteinContent, " ") as ?proteinStr)
    BIND(xsd:double(?proteinStr) as ?protein)

    ?nutritionNode schema:carbohydrateContent ?carbContent .
    BIND(strbefore(?carbContent, " ") as ?carbStr)
    BIND(xsd:double(?carbStr) as ?carbs)

    ?nutritionNode schema:sugarContent ?sugarContent .
    BIND(strbefore(?sugarContent, " ") as ?sugarStr)
    BIND(xsd:double(?sugarStr) as ?sugars)

    ?nutritionNode schema:sodiumContent ?saltContent .
    BIND(strbefore(?saltContent, " ") as ?saltStr)
    BIND(xsd:double(?saltStr) as ?salt)

    ?nutritionNode schema:fatContent ?fatContent .
    BIND(strbefore(?fatContent, " ") as ?fatStr)
    BIND(xsd:double(?fatStr) as ?fat)

    ?nutritionNode schema:saturatedFatContent ?satContent .
    BIND(strbefore(?satContent, " ") as ?satStr)
    BIND(xsd:double(?satStr) as ?sat)

    ?nutritionNode schema:fiberContent ?fiberContent .
    BIND(strbefore(?fiberContent, " ") as ?fiberStr)
    BIND(xsd:double(?fiberStr) as ?fiber)

    BIND(0 as ?score0)
    
    BIND(?score0 + if(?protein <= 50, 1, 0) as ?score1)
    BIND(?score1 + if(?carbs <= 130, 1, 0) as ?score2)
    BIND(?score2 + if(?sugars <= 90, 1, 0) as ?score3)
    BIND(?score3 + if(?salt <= 6000, 1, 0) as ?score4)
    BIND(?score4 + if(?fat <= 70, 1, 0) as ?score5)
    BIND(?score5 + if(?sat <= 20, 1, 0) as ?score6)
    BIND(?score6 + if(?fiber >= 25, 1, 0) as ?score7)
}